plugins {
  id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'eclipse'
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

version = "0.1.5"
dependencies {
    compile 'com.android.support:support-v4:23.1.0'
}

apply from: 'https://raw.githubusercontent.com/simonpoole/gradle-tasks/master/eclipse-android'

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 25
    }
    
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

ext {
	// hardwiring this is ugly but it does work
    releaseSource = file('src/main/java/ch/poole/rangebar')
    releaseJavadoc = file('build/docs/javadoc')
}

android.libraryVariants.all { variant ->
  task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
    description "Generates Javadoc for $variant.name."
    group "Documentation"
    source = variant.javaCompile.source
    classpath = files(variant.javaCompile.classpath.files) + files(android.bootClasspath) + files(variant.javaCompile.destinationDir)
    options.links("http://docs.oracle.com/javase/7/docs/api/");
    options.links("http://d.android.com/reference/");
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
  }
}

task javadocJar(type: Jar, dependsOn: 'generateReleaseJavadoc') {
	from releaseJavadoc
	classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
	from releaseSource
	classifier = 'sources'
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        rangebar(MavenPublication) {
            groupId 'ch.poole'
            artifactId 'rangebar'
			artifact sourcesJar 
			artifact javadocJar 
			artifact("$buildDir/outputs/aar/rangebar-release.aar")
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['rangebar']
    pkg {
        repo = 'div'
        name = 'rangebar'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/simonpoole/material-range-bar.git'
        version {
        	name = "${project.version}"
			released  = new Date()
			gpg {
				sign = true //Determines whether to GPG sign the files. The default is false
				passphrase = System.getenv('BINTRAY_PASSPHRASE')
			}
        }
    }
}